<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="EM_PalletConveyor" Id="{53c7f34e-04c1-49dd-bb7e-a79d410146b3}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK EM_PalletConveyor EXTENDS EM_Base IMPLEMENTS I_EM_PalletConveyor

VAR_INPUT
//Inputs
	i_ref_stSettings : REFERENCE TO ST_SettingsPalletConveyor;
	
	// External interlocks (from outside this EM)
	i_stExtIlckConvFwd : ST_Interlock;
	i_stExtIlckConvBwd : ST_Interlock;
	
	i_eCtrlState : E_CtrlState;
	
//??	i_bSta2HC_Active : BOOL;
END_VAR

VAR_OUTPUT
	q_bStaWarning : BOOL;
END_VAR

VAR
	s_eActionSelection : E_ActionSelection;
	
	//Interlocks
	s_fbIlckHandlerConveyorBackward : FB_InterlockHandler;
	s_fbIlckHandlerConveyorForward : FB_InterlockHandler;
	
	s_fbIlckHandlerCenteringIdle : FB_InterlockHandler;
	s_fbIlckHandlerCenteringCenter : FB_InterlockHandler;
	
//------------------------------------------//
	//Control Modules (Devices)
	s_cmPalletConveyor : CM_MOVITRAC_B;
	s_cmCenteringUnit : CM_ValveWithDoubleFB; // Off = Idle, On = Center
//------------------------------------------//

//------------------------------------------//
//Inputs
	s_diStaAtInfeedPositionRear : FB_DigInput;
	s_diStaAtInfeedPositionFront : FB_DigInput;
	s_diStaAtCenteringPosition : FB_DigInput;
	s_diStaAtDockingPosition : FB_DigInput;
	s_diStaDoorOpened : FB_DigInput;
//------------------------------------------//

//------------------------------------------//
//Outputs

//------------------------------------------//

	//Requests
	s_bReqTB_ToInfeed : BOOL := FALSE;
	s_bReqTB_ToCenter : BOOL := FALSE;
	s_bReqTB_ToDocking : BOOL := FALSE;
	s_bReqCenteringTB : BOOL := FALSE;
	
	//States
	s_bStaBrakeReleased : BOOL := FALSE;
	s_bStaDriveError : BOOL := FALSE;
	
	s_bStaAtInfeedRearPosition : BOOL := FALSE;
	s_bStaAtInfeedFrontPosition : BOOL := FALSE;
	s_bStaAtCenteringPosition : BOOL := FALSE;
	s_bStaAtDockingPosition : BOOL := FALSE;
	s_bStaDoorOpened : BOOL := FALSE;
	
	s_bStaCenteringCenter : BOOL := FALSE;
	s_bStaCenteringIdle : BOOL := FALSE;
	
	//Properties
	s_bStaTB_AtInfeed : BOOL;
	s_bStaTB_AtCentering : BOOL;
	s_bStaTB_AtDocking : BOOL;
	s_bStaTB_CenteringDone : BOOL;
	
	//Interlockes
	s_bIlckConveyorBackward : BOOL := TRUE;
	s_bIlckConveyorForward : BOOL := TRUE;
	
	s_bIlckCenteringToIdle : BOOL := TRUE;
	s_bIlckCenteringToCenter : BOOL := TRUE;
	
	//Alarms
	s_sReason : STRING;
	s_sMessage : STRING;
	s_fbAlarmHandlerTravelTimeOut : FB_AlarmHandler;
	s_bErrTravelTimeOut : BOOL;
	
	s_fbAlarmHandlerCmdNotAllowed : FB_AlarmHandler;
	s_bErrCmdNotAllowed : BOOL;
	
	//Warnings
	s_fbAlarmHandlerOuterDoorNotOpened : FB_AlarmHandler;
	s_bWrnOuterDoorNotOpened : BOOL;
	
	s_bStaWarning : BOOL;
	
	//Params
	s_fbParamSpeedManual : FB_HmiParamHandler;
	s_fbParamSpeedGoToInfeed : FB_HmiParamHandler;
	s_fbParamSpeedGoToCentering : FB_HmiParamHandler;
	s_fbParamSpeedGoToDocking : FB_HmiParamHandler;
	
	s_fbParamDistanceToInfeed : FB_HmiParamHandler;
	s_fbParamDistanceToCentering : FB_HmiParamHandler;
	s_fbParamDistanceToDocking : FB_HmiParamHandler;
	
	s_fbParamDistanceOuterDoorCheck : FB_HmiParamHandler;
	
	s_fbParamTravelTollerance : FB_HmiParamHandler;
	
	s_fbParamCenteringDuration : FB_HmiParamHandler;
	s_fbParamTimeoutCenteringToCenter : FB_HmiParamHandler;
	s_fbParamTimeoutCenteringToIdle : FB_HmiParamHandler;
	
	//Sequencers
	s_eSequence : E_SeqTransport;
	s_fbSeqControl : FB_SeqControl;
	
	s_ePreviousStep : E_SeqTransport;
	s_eResumeStep : E_SeqTransport;
	s_sResumeStep : STRING;
	
	s_eTmpLocation : E_Location;
	s_eTmpCenteringDone : BOOL;
	
	s_tMaxTravelTime : TIME;
	s_tDoorCheckTime : TIME;
	s_bDoorCheckTimeDisabled : BOOL := FALSE;
	
//-- Intergrated in Transport sequencer	
//--	s_eSeqCentering : E_SeqCentering;
//--	s_fbSeqCenteringControl : FB_SeqControl;
	
	//Hmi
	//Buttons
	s_fbHmiBtnTB_ToInfeed : FB_HmiBtn;
	s_fbHmiBtnTB_ToCentering : FB_HmiBtn;
	s_fbHmiBtnTB_ToDocking : FB_HmiBtn;
	s_fbHmiBtnTB_Centering : FB_HmiBtn;
	
	//Leds
	s_fbHmiLedInfeedRearPosition : FB_HmiLed;
	s_fbHmiLedInfeedFrontPosition : FB_HmiLed;
	s_fbHmiLedCenteringPosition : FB_HmiLed;
	s_fbHmiLedDockingPosition : FB_HmiLed;
	s_fbHmiLedDoorOpened : FB_HmiLed;
	
	s_fbHmiLedCenteringIdle	: FB_HmiLed;
	s_fbHmiLedCenteringCenter : FB_HmiLed;
	
	{warning '#TODO: Het zou beter zijn om dit onder te brengen in de onderstaande control modules'}
	s_bHmiForceOutputEnabled : BOOL;
	s_bHmiForceOutputStartEnabled : BOOL;
	
END_VAR

VAR PERSISTENT //Variables below will be returned during: Power Cycle, Active Configuration, Download, Cold Restart
//--	s_eLastStepCenteringPersistent : E_SeqCentering := E_SeqCentering.READY;
	s_eLastStepTransportPersistent : E_SeqTransport := E_SeqTransport.READY;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
SUPER^();
]]></ST>
    </Implementation>
    <Folder Name="Base" Id="{067ba958-c4fb-4bce-8177-9625e918c776}" />
    <Method Name="_CalcMaxTravelTime" Id="{124056bd-8750-438f-b5d1-d60729a56962}">
      <Declaration><![CDATA[METHOD PROTECTED _CalcMaxTravelTime : TIME
VAR_INPUT
	i_fBaseRampTime : LREAL; //Base ramp-up time (setting frequency convertor), to reach 3000rpm, 200mm/s
	i_fBaseSpeed : LREAL; //Base max. speed (setting frequency converor), 2000mm/s
	i_TargetSpeed : LREAL;
	i_TargetDistance : LREAL;
	i_Tollerance : LREAL;
END_VAR

VAR
	s_fRampsTime : LREAL;
	s_fRampDistance : LREAL;
	s_fRemainingDistance : LREAL;
	s_fRemainingTime : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF i_fBaseSpeed > 0 THEN
	s_fRampsTime := (i_TargetSpeed / i_fBaseSpeed) * i_fBaseRampTime;
END_IF

s_fRampDistance := (i_TargetSpeed / 2) * s_fRampsTime; 

s_fRemainingDistance := i_TargetDistance - s_fRampDistance;

IF i_TargetSpeed > 0 THEN
	s_fRemainingTime := s_fRemainingDistance / i_TargetSpeed;
END_IF

_CalcMaxTravelTime := LREAL_TO_TIME( (s_fRampsTime + s_fRemainingTime) * i_Tollerance * 1000 );
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Alarms" Id="{aee2feff-cbd9-4478-9815-a567a5153a13}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Alarms : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^._m_Alarms() THEN
	RETURN;
END_IF

// Alarm Handlers ( config --> init() )
s_fbAlarmHandlerTravelTimeOut(
	i_bStaActive := s_bErrTravelTimeOut, 
	i_sAlarmText := CONCAT(i_sEmName, CONCAT(': Travel error: ', s_sReason)));
	
s_fbAlarmHandlerOuterDoorNotOpened(i_bStaActive := s_bWrnOuterDoorNotOpened);

s_fbAlarmHandlerCmdNotAllowed(
	i_bStaActive := s_bErrCmdNotAllowed,
	i_sAlarmText := CONCAT(i_sEmName, CONCAT(': ', s_sMessage)));

// Reset warnings
IF i_bCmdReset THEN
	s_bErrTravelTimeOut := FALSE;
	s_bErrCmdNotAllowed := FALSE;
	s_bWrnOuterDoorNotOpened := FALSE;
END_IF

s_bStaError :=
	s_cmPalletConveyor.p_bStaError
	OR s_cmCenteringUnit.p_bStaError
	OR s_fbAlarmHandlerTravelTimeOut.q_bStaActive
	OR s_fbAlarmHandlerCmdNotAllowed.q_bStaActive;

s_bStaWarning :=
	s_fbAlarmHandlerOuterDoorNotOpened.q_bStaActive
	OR s_cmPalletConveyor.p_bStaWarning
	OR s_cmCenteringUnit.p_bStaWarning;
	
_m_Alarms := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_AutomaticControl" Id="{bf60d7a3-961e-4fdb-af02-f325c49fcd52}">
      <Declaration><![CDATA[METHOD PROTECTED _m_AutomaticControl : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
_m_Sequence();

//--centering functionality is build into the transport sequencer
//--_m_SeqCentering();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_CmdCenteringTB" Id="{ff42404a-08cf-4a4b-a12c-748d8f3fd351}">
      <Declaration><![CDATA[METHOD PROTECTED _m_CmdCenteringTB : BOOL
VAR_INPUT
	i_refReason : REFERENCE TO STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

IF s_bIlckCenteringToCenter THEN
	i_refReason := CONCAT('Cmd Centering: Interlocked - ', s_cmCenteringUnit.p_sInterlockedReasonOn);
	_m_CmdCenteringTB := FALSE;
	s_bErrCmdNotAllowed := TRUE;
ELSE
	i_refReason := '-';
	_m_CmdCenteringTB := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_CmdTB_ToCenter" Id="{9a04d893-3f3d-441b-875a-c54a1fc8d77d}">
      <Declaration><![CDATA[METHOD PROTECTED _m_CmdTB_ToCenter : BOOL
VAR_INPUT
	i_refReason : REFERENCE TO STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

IF s_bIlckConveyorForward THEN
	i_refReason := CONCAT('Cmd To Center: Interlocked - ', s_fbIlckHandlerConveyorForward.p_sReason);
	_m_CmdTB_ToCenter := FALSE;
	s_bErrCmdNotAllowed := TRUE;
ELSE
	i_refReason := '-';
	_m_CmdTB_ToCenter := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_CmdTB_ToDocking" Id="{2bfcdc46-8a0a-4837-b71a-35dd909a0ae6}">
      <Declaration><![CDATA[METHOD PROTECTED _m_CmdTB_ToDocking : BOOL
VAR_INPUT
	i_refReason : REFERENCE TO STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

IF s_bIlckConveyorForward THEN
	i_refReason := CONCAT('Cmd To Docking: Interlocked - ', s_fbIlckHandlerConveyorForward.p_sReason);
	_m_CmdTB_ToDocking := FALSE;
	s_bErrCmdNotAllowed := TRUE;
ELSE
	i_refReason := '-';
	_m_CmdTB_ToDocking := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_CmdTB_ToInfeed" Id="{cf16f0e7-53a6-4bd4-b559-f1ce284b5a59}">
      <Declaration><![CDATA[METHOD PROTECTED _m_CmdTB_ToInfeed : BOOL
VAR_INPUT
	i_refReason : REFERENCE TO STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

IF s_bIlckConveyorBackward THEN
	i_refReason := CONCAT('Cmd To Infeed: Interlocked - ', s_fbIlckHandlerConveyorBackward.p_sReason);
	_m_CmdTB_ToInfeed := FALSE;
	s_bErrCmdNotAllowed := TRUE;
ELSE
	i_refReason := '-';
	_m_CmdTB_ToInfeed := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Devices" Id="{a5afe549-ba9e-46b9-b9ab-569cdd3f8cbd}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Devices : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^._m_Devices() THEN
	RETURN;
END_IF

s_cmPalletConveyor(
	i_bReqEnable := i_bReqEnable,
	i_eCtrlMode := i_eCtrlMode,
	i_ref_eCtrlState := i_eCtrlState,
	i_bCmdReset := i_bCmdReset,
	i_bIclkForward := s_bIlckConveyorForward AND (i_eCtrlMode <> E_CtrlMode.IO),
	i_bIclkBackward := s_bIlckConveyorBackward AND (i_eCtrlMode <> E_CtrlMode.IO));

s_bStaBrakeReleased := s_cmPalletConveyor.p_bStaBrakeReleased;
s_bStaDriveError := s_cmPalletConveyor.p_bStaError; 

s_cmCenteringUnit(
	i_bReqEnable := i_bReqEnable,
	i_eCtrlMode := i_eCtrlMode,
	i_ref_eCtrlState := i_eCtrlState,
	i_bCmdReset := i_bCmdReset,
	i_bIclkOn := s_bIlckCenteringToCenter AND (i_eCtrlMode <> E_CtrlMode.IO),
	i_bIclkOff := FALSE);
	
s_bStaCenteringCenter := s_cmCenteringUnit.p_bStaIsOn;
s_bStaCenteringIdle := s_cmCenteringUnit.p_bStaIsOff;

s_diStaAtInfeedPositionRear();
s_diStaAtInfeedPositionFront();
s_diStaAtCenteringPosition();
s_diStaAtDockingPosition();
s_diStaDoorOpened();

s_bStaAtInfeedRearPosition := s_diStaAtInfeedPositionRear.p_bStaInputDelayed;
s_bStaAtInfeedFrontPosition := s_diStaAtInfeedPositionFront.p_bStaInputDelayed;
s_bStaAtCenteringPosition := s_diStaAtCenteringPosition.p_bStaInputDelayed;
s_bStaAtDockingPosition := s_diStaAtDockingPosition.p_bStaInputDelayed;
s_bStaDoorOpened := s_diStaDoorOpened.p_bStaInputDelayed;

_m_Devices := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Hmi" Id="{afc38d7b-1a6b-4465-996c-ac4ea040e152}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Hmi : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^._m_Hmi() THEN
	RETURN;
END_IF

//Buttons
s_fbHmiBtnTB_ToInfeed();
s_fbHmiBtnTB_ToCentering();
s_fbHmiBtnTB_ToDocking();
s_fbHmiBtnTB_Centering();

//Leds
s_fbHmiLedInfeedRearPosition(i_bStaLed := s_bStaAtInfeedRearPosition);
s_fbHmiLedInfeedFrontPosition(i_bStaLed := s_bStaAtInfeedFrontPosition);
s_fbHmiLedCenteringPosition(i_bStaLed := s_bStaAtCenteringPosition);
s_fbHmiLedDockingPosition(i_bStaLed := s_bStaAtDockingPosition);
s_fbHmiLedDoorOpened(i_bStaLed := s_bStaDoorOpened);

s_fbHmiLedCenteringCenter(i_bStaLed := s_bStaCenteringCenter);
s_fbHmiLedCenteringIdle(i_bStaLed := s_bStaCenteringIdle);

_m_Hmi := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Init" Id="{a6194918-083d-460c-8c8c-f7fff673bb61}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Init : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^._m_Init() THEN
	RETURN;
END_IF

//Control Modules
s_cmPalletConveyor.i_bReqEnable := FALSE;
s_cmPalletConveyor.i_eCtrlMode := E_CtrlMode.IDLE;
s_cmPalletConveyor.i_sCmName := 'CM Pallet Conveyor';
s_cmPalletConveyor.i_stSettings.tResetPulseDuration := T#1S;
s_cmPalletConveyor.i_stSettings.tBrakeReleasedDelay := T#1S;
s_cmPalletConveyor.i_stSettings.tMotorStoppedDelay := T#3S;
s_cmPalletConveyor.i_stSettings.rMaxTargetSpeed := 200.0; // mm/s
s_cmPalletConveyor.p_sCfgFwdName := 'Forward';
s_cmPalletConveyor.p_sCfgBckName := 'Backward';

s_cmCenteringUnit.i_bReqEnable := FALSE;
s_cmCenteringUnit.i_eCtrlMode := E_CtrlMode.IDLE;
s_cmCenteringUnit.i_sCmName := 'CM Centering Unit';
s_cmCenteringUnit.i_stSettings.tValveOnTimeout := T#5S;
s_cmCenteringUnit.i_stSettings.tValveOffTimeout := T#5S;

s_cmCenteringUnit.p_bCfgBiStableValve := FALSE; // 5/2 monostable
s_cmCenteringUnit.p_bCfgReverseValve := FALSE;
s_cmCenteringUnit.p_sCfgOffName := 'Rest';
s_cmCenteringUnit.p_sCfgOnName := 'Center';
s_cmCenteringUnit.p_sCfgFB1Name := 'Rear';
s_cmCenteringUnit.p_sCfgFB2Name := 'Front';

s_diStaAtInfeedPositionRear.i_stSettings.tmInputDelay := T#0MS;
s_diStaAtInfeedPositionFront.i_stSettings.tmInputDelay := T#0MS;
s_diStaAtCenteringPosition.i_stSettings.tmInputDelay := T#0MS;
s_diStaAtDockingPosition.i_stSettings.tmInputDelay := T#1000MS;
s_diStaDoorOpened.i_stSettings.tmInputDelay := T#0MS;

//Parameters (default values read from file)
s_fbParamSpeedManual.i_sName := CONCAT(i_sEmName, ': Speed Manual');
s_fbParamSpeedManual.i_nArrayFolderIndex := 1;
s_fbParamSpeedManual.i_bIsNumber := TRUE;

s_fbParamSpeedGoToInfeed.i_sName := CONCAT(i_sEmName, ': Speed Go To Infeed');
s_fbParamSpeedGoToInfeed.i_nArrayFolderIndex := 1;
s_fbParamSpeedGoToInfeed.i_bIsNumber := TRUE;

s_fbParamSpeedGoToCentering.i_sName := CONCAT(i_sEmName, ': Speed Go To Centering');
s_fbParamSpeedGoToCentering.i_nArrayFolderIndex := 1;
s_fbParamSpeedGoToCentering.i_bIsNumber := TRUE;

s_fbParamSpeedGoToDocking.i_sName := CONCAT(i_sEmName, ': Speed Go To Docking');
s_fbParamSpeedGoToDocking.i_nArrayFolderIndex := 1;
s_fbParamSpeedGoToDocking.i_bIsNumber := TRUE;

s_fbParamDistanceToInfeed.i_sName := CONCAT(i_sEmName, ': Distance To Infeed');
s_fbParamDistanceToInfeed.i_nArrayFolderIndex := 1;
s_fbParamDistanceToInfeed.i_bIsNumber := TRUE;

s_fbParamDistanceToCentering.i_sName := CONCAT(i_sEmName, ': Distance To Centering');
s_fbParamDistanceToCentering.i_nArrayFolderIndex := 1;
s_fbParamDistanceToCentering.i_bIsNumber := TRUE;;

s_fbParamDistanceToDocking.i_sName := CONCAT(i_sEmName, ': Distance To Docking');
s_fbParamDistanceToDocking.i_nArrayFolderIndex := 1;
s_fbParamDistanceToDocking.i_bIsNumber := TRUE;

s_fbParamDistanceOuterDoorCheck.i_sName := CONCAT(i_sEmName, ': Distance Outer Door Check');
s_fbParamDistanceOuterDoorCheck.i_nArrayFolderIndex := 1;
s_fbParamDistanceOuterDoorCheck.i_bIsNumber := TRUE;

s_fbParamTravelTollerance.i_sName := CONCAT(i_sEmName, ': Travel Tollerance');
s_fbParamTravelTollerance.i_nArrayFolderIndex := 1;
s_fbParamTravelTollerance.i_bIsNumber := TRUE;

s_fbParamCenteringDuration.i_sName := CONCAT(i_sEmName, ': Centering Duration');
s_fbParamCenteringDuration.i_nArrayFolderIndex := 1;
s_fbParamCenteringDuration.i_bIsNumber := TRUE;

s_fbParamTimeoutCenteringToCenter.i_sName := CONCAT(i_sEmName, ': Timeout Centering To Center');
s_fbParamTimeoutCenteringToCenter.i_nArrayFolderIndex := 1;
s_fbParamTimeoutCenteringToCenter.i_bIsNumber := TRUE;

s_fbParamTimeoutCenteringToIdle.i_sName := CONCAT(i_sEmName, ': Timeout Centering To Idle');
s_fbParamTimeoutCenteringToIdle.i_nArrayFolderIndex := 1;
s_fbParamTimeoutCenteringToIdle.i_bIsNumber := TRUE;

//Interlock strings
//Conveyor
//[0].sReason --> External, dynamical will be written in m_input
s_fbIlckHandlerConveyorBackward.i_stInterlockList.arr_Interlocks[1].sReason := 'Utilities not OK';
s_fbIlckHandlerConveyorBackward.i_stInterlockList.arr_Interlocks[2].sReason := 'Centering not at idle-position';
s_fbIlckHandlerConveyorBackward.i_stInterlockList.arr_Interlocks[3].sReason := 'Already at infeed position';

//[0].sReason --> External, dynamical will be written in m_input
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[1].sReason := 'Utilities not OK';
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[2].sReason := 'Centering not at idle-position';
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[3].sReason := 'Outer TB door not detected';
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[4].sReason := 'Centring position occupied';
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[5].sReason := 'Docking position occupied';

//Centering
s_fbIlckHandlerCenteringIdle.i_stInterlockList.arr_Interlocks[0].sReason := 'Utilities not OK';

s_fbIlckHandlerCenteringCenter.i_stInterlockList.arr_Interlocks[0].sReason := 'Utilities not OK';
s_fbIlckHandlerCenteringCenter.i_stInterlockList.arr_Interlocks[1].sReason := 'Conveyor not stopped';

//Alarm config
s_fbAlarmHandlerTravelTimeOut.i_eAlarmLevel := E_AlarmLevel.Warning;

s_fbAlarmHandlerOuterDoorNotOpened.i_sAlarmText := CONCAT(i_sEmName, ': Outer-door open not detected');
s_fbAlarmHandlerOuterDoorNotOpened.i_eAlarmLevel := E_AlarmLevel.Warning;

s_fbAlarmHandlerCmdNotAllowed.i_eAlarmLevel := E_AlarmLevel.Alarm;

//Hmi
s_fbHmiBtnTB_ToInfeed.p_eButtontype := E_HmiButtonType.PUSHBUTTON;
s_fbHmiBtnTB_ToInfeed.p_bStaVisible := FALSE;
s_fbHmiBtnTB_ToInfeed.p_bStaEnabled := FALSE;
s_fbHmiBtnTB_ToInfeed.p_sBtnText := 'TB To Infeed Position';

s_fbHmiBtnTB_ToCentering.p_eButtontype := E_HmiButtonType.PUSHBUTTON;
s_fbHmiBtnTB_ToCentering.p_bStaVisible := FALSE;
s_fbHmiBtnTB_ToCentering.p_bStaEnabled := FALSE;
s_fbHmiBtnTB_ToCentering.p_sBtnText := 'TB To Centering Position';

s_fbHmiBtnTB_ToDocking.p_eButtontype := E_HmiButtonType.PUSHBUTTON;
s_fbHmiBtnTB_ToDocking.p_bStaVisible := FALSE;
s_fbHmiBtnTB_ToDocking.p_bStaEnabled := FALSE;
s_fbHmiBtnTB_ToDocking.p_sBtnText := 'TB To Docking Position';

s_fbHmiBtnTB_Centering.p_eButtontype := E_HmiButtonType.PUSHBUTTON;
s_fbHmiBtnTB_Centering.p_bStaVisible := FALSE;
s_fbHmiBtnTB_Centering.p_bStaEnabled := FALSE;
s_fbHmiBtnTB_Centering.p_sBtnText := 'Centering TB';

s_fbHmiLedInfeedRearPosition.p_bStaVisible := FALSE;
s_fbHmiLedInfeedRearPosition.p_sToHmiDescriptionLed := 'Infeed Rear Position';

s_fbHmiLedInfeedFrontPosition.p_bStaVisible := FALSE;
s_fbHmiLedInfeedFrontPosition.p_sToHmiDescriptionLed := 'Infeed Front Position';

s_fbHmiLedCenteringPosition.p_bStaVisible := FALSE;
s_fbHmiLedCenteringPosition.p_sToHmiDescriptionLed := 'Center Position';

s_fbHmiLedDockingPosition.p_bStaVisible := FALSE;
s_fbHmiLedDockingPosition.p_sToHmiDescriptionLed := 'Docking Position';

s_fbHmiLedDoorOpened.p_bStaVisible := FALSE;
s_fbHmiLedDoorOpened.p_sToHmiDescriptionLed := 'Door Opened';

s_fbHmiLedCenteringIdle.p_bStaVisible := FALSE;
s_fbHmiLedCenteringIdle.p_sToHmiDescriptionLed := 'Centering Idle';

s_fbHmiLedCenteringCenter.p_bStaVisible := FALSE;
s_fbHmiLedCenteringCenter.p_sToHmiDescriptionLed := 'Centering Center';
	
_m_Init := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Inputs" Id="{6338a124-287d-498f-bd5c-dc8edaa7899d}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Inputs : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^._m_Inputs() THEN
	RETURN;
END_IF

//Interlocks
_m_Interlocks();

//??
(*
//TB States (only use data if a valid ID is pressent else use sensor states)
IF __ISVALIDREF(i_refTB) AND (i_refTB.p_nId > 0) THEN 
	s_bStaTB_CenteringDone := i_refTB.p_bCenteringDone;
	s_bStaTB_AtInfeed := i_refTB.p_eActLocation = E_Location.INFEED;
	s_bStaTB_AtCentering := i_refTB.p_eActLocation = E_Location.CENTERING;
	s_bStaTB_AtDocking := i_refTB.p_eActLocation = E_Location.DOCKING;
ELSE
*)
	s_bStaTB_CenteringDone := s_eTmpCenteringDone;
	s_bStaTB_AtInfeed := s_bStaAtInfeedRearPosition OR s_bStaAtInfeedFrontPosition;
	s_bStaTB_AtCentering := s_bStaAtCenteringPosition;
	s_bStaTB_AtDocking := s_bStaAtDockingPosition;
//??END_IF

_m_Inputs := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Interlocks" Id="{5d680404-6254-4d7f-ba59-60012ab8115d}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Interlocks
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
//Conveyor/////////////////////////////////////////////////////////////////////////////////////////
s_fbIlckHandlerConveyorBackward.i_stInterlockList.arr_Interlocks[0] := i_stExtIlckConvBwd;
s_fbIlckHandlerConveyorBackward.i_stInterlockList.arr_Interlocks[1].bState := NOT(i_bReqEnable);
s_fbIlckHandlerConveyorBackward.i_stInterlockList.arr_Interlocks[2].bState := NOT(s_bStaCenteringIdle);
//--fbIlckHandlerConveyorBackward.i_stInterlockList.arr_Interlocks[3].bState := s_bStaAtInfeedRearPosition;	//Not sure if this is really a interlock?
s_fbIlckHandlerConveyorBackward();

s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[0] := i_stExtIlckConvFwd;
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[1].bState := NOT(i_bReqEnable);
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[2].bState := NOT(s_bStaCenteringIdle);
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[3].bState := NOT(s_bStaDoorOpened) AND s_bStaAtInfeedRearPosition;
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[4].bState := s_bStaAtCenteringPosition AND (s_bStaAtInfeedFrontPosition OR s_bStaAtInfeedRearPosition); //Not sure if this is really a interlock?
s_fbIlckHandlerConveyorForward.i_stInterlockList.arr_Interlocks[5].bState := s_bStaAtDockingPosition AND FALSE; //Not sure if this is really a interlock?
s_fbIlckHandlerConveyorForward();

s_bIlckConveyorBackward := s_fbIlckHandlerConveyorBackward.p_bInterlocked;
s_bIlckConveyorForward := s_fbIlckHandlerConveyorForward.p_bInterlocked;

IF s_bIlckConveyorBackward THEN
	s_cmPalletConveyor.p_sInterlockedReasonBackward := s_fbIlckHandlerConveyorBackward.p_sReason;
ELSE
	s_cmPalletConveyor.p_sInterlockedReasonBackward := '-';
END_IF

IF s_bIlckConveyorForward THEN
	s_cmPalletConveyor.p_sInterlockedReasonForward := s_fbIlckHandlerConveyorForward.p_sReason;
ELSE
	s_cmPalletConveyor.p_sInterlockedReasonForward := '-';
END_IF

//Centering////////////////////////////////////////////////////////////////////////////////////////
s_fbIlckHandlerCenteringIdle.i_stInterlockList.arr_Interlocks[0].bState := NOT(i_bReqEnable);
s_fbIlckHandlerCenteringIdle();

s_fbIlckHandlerCenteringCenter.i_stInterlockList.arr_Interlocks[0].bState := NOT(i_bReqEnable);
s_fbIlckHandlerCenteringCenter.i_stInterlockList.arr_Interlocks[1].bState := NOT(s_cmPalletConveyor.p_bStaMotorStopped);
s_fbIlckHandlerCenteringCenter();

s_bIlckCenteringToIdle := s_fbIlckHandlerCenteringIdle.p_bInterlocked;
s_bIlckCenteringToCenter := s_fbIlckHandlerCenteringCenter.p_bInterlocked;
 
//Centring
IF s_bIlckCenteringToIdle THEN
	s_cmCenteringUnit.p_sInterlockedReasonOff := s_fbIlckHandlerCenteringIdle.p_sReason;
ELSE
	s_cmCenteringUnit.p_sInterlockedReasonOff := '-';
END_IF

IF s_bIlckCenteringToCenter THEN
	s_cmCenteringUnit.p_sInterlockedReasonOn := s_fbIlckHandlerCenteringCenter.p_sReason;
ELSE
	s_cmCenteringUnit.p_sInterlockedReasonOn := '-';
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Outputs" Id="{dde91033-d57b-467b-929e-41cce55dae6e}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Outputs : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^._m_Outputs() THEN
	RETURN;
END_IF

q_bStaWarning := s_bStaWarning;

_m_Outputs := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Params" Id="{b7688a2b-8ea9-42be-9d82-c5b14e9212e5}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PROTECTED _m_Params : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^._m_Params() THEN
	RETURN;
END_IF

//Paramaters
s_fbParamSpeedManual();
s_fbParamSpeedGoToInfeed();
s_fbParamSpeedGoToCentering();
s_fbParamSpeedGoToDocking();
s_fbParamDistanceToInfeed();
s_fbParamDistanceToCentering();
s_fbParamDistanceToDocking();
s_fbParamDistanceOuterDoorCheck();
s_fbParamTravelTollerance();
s_fbParamCenteringDuration();
s_fbParamTimeoutCenteringToCenter();
s_fbParamTimeoutCenteringToIdle();

//--
(*
s_fbParamSpeedManual(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Speed Manual'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamSpeedGoToInfeed(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Speed Go To Infeed'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamSpeedGoToCentering(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Speed Go To Centering'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamSpeedGoToDocking(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Speed Go To Docking'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamDistanceToInfeed(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Distance To Infeed'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamDistanceToCentering(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Distance To Centering'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamDistanceToDocking(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Distance To Docking'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamDistanceOuterDoorCheck(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Distance Outer Door Check'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamTravelTollerance(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Travel Tollerance'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamCenteringDuration(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Centering Duration'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamTimeoutCenteringToCenter(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Timeout Centering To Center'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
	
s_fbParamTimeoutCenteringToIdle(
	i_fDefaultValue :=,
	i_sName := CONCAT(i_sEmName, ': Timeout Centering To Idle'),
	i_nArrayFolderIndex := 1,
	i_bIsNumber := TRUE);
*)

//Write to settings
i_ref_stSettings.fSpeedManual := s_fbParamSpeedManual.p_fValue;
i_ref_stSettings.fSpeedGoToInfeed := s_fbParamSpeedGoToInfeed.p_fValue;
i_ref_stSettings.fSpeedGoToCentering := s_fbParamSpeedGoToCentering.p_fValue;
i_ref_stSettings.fSpeedGoToDocking := s_fbParamSpeedGoToDocking.p_fValue;
i_ref_stSettings.fDistanceToInfeed := s_fbParamDistanceToInfeed.p_fValue;
i_ref_stSettings.fDistanceToCentering := s_fbParamDistanceToCentering.p_fValue;
i_ref_stSettings.fDistanceToDocking := s_fbParamDistanceToDocking.p_fValue;
i_ref_stSettings.fDistanceOuterDoorCheck := s_fbParamDistanceOuterDoorCheck.p_fValue;
i_ref_stSettings.fTravelTollerance := s_fbParamTravelTollerance.p_fValue;
i_ref_stSettings.tCenteringDuration := LREAL_TO_TIME(s_fbParamCenteringDuration.p_fValue);
i_ref_stSettings.tTimeoutCenteringToCenter := LREAL_TO_TIME(s_fbParamTimeoutCenteringToCenter.p_fValue);
i_ref_stSettings.tTimeoutCenteringToIdle := LREAL_TO_TIME(s_fbParamTimeoutCenteringToIdle.p_fValue);

_m_Params := TRUE;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_SeqCentering" Id="{9ea29294-eb8f-4625-80fc-76ae8a3e8898}">
      <Declaration><![CDATA[METHOD PROTECTED _m_SeqCentering : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
s_fbSeqCenteringControl(
	i_nSeqStep := s_eSeqCentering,
	i_sStepName := TO_STRING(s_eSeqCentering),
	i_bCmdReset := i_bCmdReset
					AND (s_eSeqCentering = E_SeqCentering.ERROR) // Only reset/resume sequencer from ERROR state
					AND NOT(s_cmCenteringUnit.p_bStaError)
					AND NOT(s_cmPalletConveyor.p_bStaError)
					AND i_bReqEnable,
	i_bStaError := s_bStaError OR NOT(i_bReqEnable));

//Abbort operation (go to stop)
IF (i_eCtrlState = E_CtrlState.STOPPING) THEN
	s_eSeqTransport := E_SeqTransport.STOP;
//--s_eSeqCentering := E_SeqCentering.MOVE_TO_IDLE;
END_IF

CASE s_eSeqCentering OF
	
	E_SeqCentering.IDLE:
		s_bReqCenteringTB := FALSE;
		
		IF (i_eCtrlState = E_CtrlState.IDLE) THEN
			s_eSeqCentering := E_SeqCentering.START;
		END_IF
		
	E_SeqCentering.START:
//--		IF s_fbSeqCenteringControl.q_tmStepTime > T#1S THEN
//--			s_eSeqCentering := E_SeqCentering.MOVE_TO_IDLE;
//--		END_IF
		
		IF i_eCtrlState = E_CtrlState.RUNNING THEN
			s_eSeqCentering := E_SeqCentering.MOVE_TO_IDLE;
		END_IF
		
	E_SeqCentering.MOVE_TO_IDLE:
		IF s_cmCenteringUnit.m_Off() THEN
			s_eSeqCentering := E_SeqCentering.AT_IDLE;
		END_IF
		
		//??IF i_bSta2HC_Active THEN
		//??	IF s_cmCenteringUnit.m_Off() THEN
		//??	s_eSeqCentering := E_SeqCentering.AT_IDLE;
		//??END_IF
		//??ELSE
		//??	s_cmCenteringUnit.m_Stop();
		//??END_IF
		
	E_SeqCentering.AT_IDLE:
//--		IF s_fbSeqCenteringControl.q_tmStepTime > T#1S THEN
		//??IF s_fbSeqCenteringControl.q_tmStepTime > T#1S AND NOT(i_bSta2HC_Active) THEN
			s_eSeqCentering := E_SeqCentering.READY;
//--		END_IF
		
	E_SeqCentering.READY:
		IF s_fbSeqCenteringControl.q_bStaEntry THEN
			i_refTB.p_bCenteringDone S= s_eTmpCenteringDone; //Explicity SET value
			s_eTmpCenteringDone := FALSE;
		END_IF
		
		IF i_eCtrlState <> E_CtrlState.RUNNING THEN
			s_eSeqCentering := E_SeqCentering.START;
		END_IF
		
		IF s_bReqCenteringTB THEN
			IF _m_CmdCenteringTB(i_refReason := s_sMessage) THEN
				s_eSeqCentering := E_SeqCentering.MOVE_TO_CENTER;
			//ELSE	If failed (command not allowed): the message will be written with the interlock reason
			END_IF
			
			s_bReqCenteringTB := FALSE;
		END_IF
		
	E_SeqCentering.MOVE_TO_CENTER:
		//++IF s_cmCenteringUnit.m_On() THEN // Temp. added time delay as extra condition to skip reed contacts (end position not reachable with test box)
		IF s_cmCenteringUnit.m_On() OR (s_fbSeqCenteringControl.q_tmStepTime >= T#1S) THEN
			s_eSeqCentering := E_SeqCentering.AT_CENTER;
		END_IF
		
		//??IF i_bSta2HC_Active THEN
		//??	IF s_cmCenteringUnit.m_On() THEN
		//??		s_eSeqCentering := E_SeqCentering.AT_CENTER;
		//??	END_IF
		//??ELSE
		//??	s_cmCenteringUnit.m_Stop();
		//??END_IF
		
	E_SeqCentering.AT_CENTER:
		IF s_fbSeqCenteringControl.q_tmStepTime > i_ref_stSettings.tCenteringDuration THEN
			s_eTmpCenteringDone := TRUE;
			s_eSeqCentering := E_SeqCentering.MOVE_TO_IDLE;
		END_IF
	
	E_SeqCentering.STOP:
		s_cmCenteringUnit.m_Off();
		
		IF s_cmCenteringUnit.p_bStaIsOff THEN
			s_eSeqCentering := E_SeqCentering.READY;
		END_IF
		
	E_SeqCentering.ERROR:
		s_cmCenteringUnit.m_Stop();
	
END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="_m_Sequence" Id="{7bf2ff54-007b-4f2a-966d-421cf756f1be}">
      <Declaration><![CDATA[METHOD PROTECTED _m_Sequence : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
s_fbSeqControl(
	i_nSeqStep := s_eSequence,
	i_sStepName := TO_STRING(s_eSequence),
	i_bCmdReset :=
		i_bCmdReset
		AND i_bReqEnable
		AND (s_eSequence = E_SeqTransport.ERROR) // Only reset/resume sequencer from ERROR state
		AND NOT(s_bStaError),
	i_bStaError := s_bStaError OR NOT(i_bReqEnable));
	
s_sResumeStep := TO_STRING(s_eResumeStep);

//Force to stop
IF (i_eCtrlState = E_CtrlState.STOPPING) THEN
	s_eSequence := E_SeqTransport.STOP;
END_IF

CASE s_eSequence OF
	E_SeqTransport.IDLE:
		s_eTmpLocation := E_Location.UNKNOWN;
		
		IF (i_eCtrlState = E_CtrlState.IDLE) THEN
			s_eSequence := E_SeqTransport.START;
		END_IF
		
//START////////////////////////////////////////////////////////////////////////////////////////////
	E_SeqTransport.START:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_cmPalletConveyor.m_Stop();
			s_cmCenteringUnit.m_Off();
			s_sMessage := 'start';
		END_IF
		
		IF s_cmPalletConveyor.p_bStaMotorStopped AND_THEN NOT(s_cmPalletConveyor.p_bStaBrakeReleased) AND s_fbSeqControl.q_tmStepTime > T#1S THEN
		//??	AND NOT(i_bSta2HC_Active) THEN
			
			IF i_eCtrlState = E_CtrlState.RUNNING THEN
				s_eSequence := E_SeqTransport.READY;
			END_IF
		END_IF
		
//READY////////////////////////////////////////////////////////////////////////////////////////////
	E_SeqTransport.READY:
		IF s_fbSeqControl.q_bStaEntry THEN
//??			i_refTB.p_eActLocation := s_eTmpLocation;
			s_eResumeStep := E_SeqTransport.READY;
			s_eTmpLocation := E_Location.UNKNOWN;
		END_IF
		
//??		i_refTB.p_bOuterDoorOpened S= s_bStaAtInfeedRearPosition AND s_bStaAtInfeedFrontPosition AND s_bStaDoorOpened;
		
		IF i_eCtrlState <> E_CtrlState.RUNNING THEN
			s_eSequence := E_SeqTransport.START;
		END_IF
		
		IF s_bReqTB_ToInfeed THEN
			
			IF _m_CmdTB_ToInfeed(i_refReason := s_sMessage) THEN
				s_eSequence := E_SeqTransport.MOVE_TO_INFEED;
			ELSE //If failed (command not allowed): the message will be written with the interlock reason
				;
			END_IF
			
			s_bReqTB_ToInfeed := FALSE;
			
		ELSIF s_bReqTB_ToCenter THEN
			
			IF _m_CmdTB_ToCenter(i_refReason := s_sMessage) THEN
				s_eSequence := E_SeqTransport.MOVE_TO_CENTERING;
			ELSE //If failed (command not allowed): the message will be written with the interlock reason
				;
			END_IF
			
			s_bReqTB_ToCenter := FALSE;
			
		ELSIF s_bReqTB_ToDocking THEN
			
			IF _m_CmdTB_ToDocking(i_refReason := s_sMessage) THEN
				s_eSequence := E_SeqTransport.MOVE_TO_DOCKING;
			ELSE //If failed (command not allowed): the message will be written with the interlock reason
				;
			END_IF
			
			s_bReqTB_ToDocking := FALSE;
			
		ELSIF s_bReqCenteringTB THEN
			
			IF _m_CmdCenteringTB(i_refReason := s_sMessage) THEN
				s_eSequence := E_SeqTransport.PUSHER_TO_CENTER;
			ELSE //If failed (command not allowed): the message will be written with the interlock reason
				;
			END_IF
			
			s_bReqCenteringTB := FALSE;
			
		END_IF
		
//MOVE TO INFEED///////////////////////////////////////////////////////////////////////////////////
	E_SeqTransport.MOVE_TO_INFEED:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_tMaxTravelTime :=
				_CalcMaxTravelTime(
					i_fBaseRampTime := 4.0,
					i_fBaseSpeed := 200.0,
					i_TargetSpeed := i_ref_stSettings.fSpeedGoToInfeed,
					i_TargetDistance := i_ref_stSettings.fDistanceToInfeed,
					i_Tollerance := i_ref_stSettings.fTravelTollerance);
					
			s_cmPalletConveyor.m_StartBackward(i_rTargetSpeed := i_ref_stSettings.fSpeedGoToInfeed);
		END_IF
		
		//??IF i_bSta2HC_Active THEN
		//??	IF s_fbSeqTransportControl.q_bStaEntry THEN
		//??		s_tMaxTravelTime := LREAL_TO_TIME((i_ref_stSettings.fDistanceToInfeed / i_ref_stSettings.fSpeedGoToInfeed) * i_ref_stSettings.fTravelTollerance * 1000);
		//??		s_cmPalletConveyor.m_StartBackward(i_rTargetSpeed := i_ref_stSettings.fSpeedGoToInfeed);
		//??	END_IF
		//??ELSE
		//??	s_eSeqTransport := E_SeqTransport.STOP;
		//??	//--s_cmPalletConveyor.m_Stop();
		//??END_IF
		
		IF s_bStaAtInfeedRearPosition AND s_bStaAtInfeedFrontPosition THEN
			s_eTmpLocation := E_Location.INFEED;
			s_eSequence := E_SeqTransport.STOP;
		ELSIF s_fbSeqControl.q_tmStepTime > s_tMaxTravelTime THEN
			s_bErrTravelTimeOut := TRUE;
			s_sReason := 'Moving to infeed - timeout';
			s_eSequence := E_SeqTransport.ERROR;
		END_IF
		
//CENTERING////////////////////////////////////////////////////////////////////////////////////////	
	E_SeqTransport.MOVE_TO_CENTERING:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_tMaxTravelTime :=
				_CalcMaxTravelTime(
					i_fBaseRampTime := 4.0,
					i_fBaseSpeed := 200.0,
					i_TargetSpeed := i_ref_stSettings.fSpeedGoToCentering,
					i_TargetDistance := i_ref_stSettings.fDistanceToCentering,
					i_Tollerance := i_ref_stSettings.fTravelTollerance);
			
			s_tDoorCheckTime := LREAL_TO_TIME((i_ref_stSettings.fDistanceOuterDoorCheck / i_ref_stSettings.fSpeedGoToCentering) * 1000);
			s_cmPalletConveyor.m_StartForward(i_rTargetSpeed := i_ref_stSettings.fSpeedGoToCentering);
		END_IF
		
		//??IF i_bSta2HC_Active THEN
		//??	IF s_fbSeqTransportControl.q_bStaEntry THEN
		//??		s_tMaxTravelTime := LREAL_TO_TIME((i_ref_stSettings.fDistanceToCentering / i_ref_stSettings.fSpeedGoToCentering) * i_ref_stSettings.fTravelTollerance * 1000);
		//??		s_tDoorCheckTime := LREAL_TO_TIME((i_ref_stSettings.fDistanceOuterDoorCheck / i_ref_stSettings.fSpeedGoToCentering) * 1000);
		//??		s_cmPalletConveyor.m_StartForward(i_rTargetSpeed := i_ref_stSettings.fSpeedGoToCentering);
		//??	END_IF
		//??ELSE
		//??	s_eSeqTransport := E_SeqTransport.STOP;
		//??	//--s_cmPalletConveyor.m_Stop();
		//??END_IF
		
		IF s_bStaAtCenteringPosition THEN
			s_bDoorCheckTimeDisabled := FALSE;
			s_eTmpLocation := E_Location.CENTERING;
			s_eSequence := E_SeqTransport.MOVE_TO_CENTERING_STOP;
			
		ELSIF s_fbSeqControl.q_tmStepTime < s_tDoorCheckTime
			AND NOT(s_bStaDoorOpened)
			AND s_bStaAtInfeedRearPosition THEN
			s_bWrnOuterDoorNotOpened := TRUE;
			s_bDoorCheckTimeDisabled := TRUE; //Disable error after first occurance (to prevent invalid retriggering at reties)
			s_eSequence := E_SeqTransport.ERROR;
			
		ELSIF s_fbSeqControl.q_tmStepTime > s_tMaxTravelTime THEN
			s_bErrTravelTimeOut := TRUE;
			s_sReason := 'Moving to Centering - timeout';
			s_eSequence := E_SeqTransport.ERROR;
			
		END_IF
		
	E_SeqTransport.MOVE_TO_CENTERING_STOP:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_cmPalletConveyor.m_Stop();
		END_IF
		
		IF s_cmPalletConveyor.p_bStaMotorStopped THEN
			s_eSequence := E_SeqTransport.PUSHER_TO_CENTER;
		END_IF
		
	E_SeqTransport.PUSHER_TO_CENTER:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_cmCenteringUnit.m_On();
		END_IF
		
		IF s_cmCenteringUnit.p_bStaIsOn THEN
			s_eSequence := E_SeqTransport.PUSHER_AT_CENTER;
		END_IF
		
	E_SeqTransport.PUSHER_AT_CENTER:
		IF s_fbSeqControl.q_tmStepTime > i_ref_stSettings.tCenteringDuration THEN
			s_eTmpCenteringDone := TRUE;
			s_eSequence := E_SeqTransport.PUSHER_TO_IDLE;
		END_IF
		
	E_SeqTransport.PUSHER_TO_IDLE:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_cmCenteringUnit.m_Off();
		END_IF
		
		IF s_cmCenteringUnit.p_bStaIsOff THEN
			s_eSequence := E_SeqTransport.READY;
		END_IF
		
//MOVE TO DOCKING//////////////////////////////////////////////////////////////////////////////////
	E_SeqTransport.MOVE_TO_DOCKING:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_tMaxTravelTime :=
				_CalcMaxTravelTime(
					i_fBaseRampTime := 4.0,
					i_fBaseSpeed := 200.0,
					i_TargetSpeed := i_ref_stSettings.fSpeedGoToDocking,
					i_TargetDistance := i_ref_stSettings.fDistanceToDocking,
					i_Tollerance := i_ref_stSettings.fTravelTollerance);
					
			s_cmPalletConveyor.m_StartForward(i_rTargetSpeed := i_ref_stSettings.fSpeedGoToDocking);
		END_IF
		
		//??IF i_bSta2HC_Active THEN
		//??	IF s_fbSeqTransportControl.q_bStaEntry THEN
		//??		s_tMaxTravelTime := LREAL_TO_TIME((i_ref_stSettings.fDistanceToDocking / i_ref_stSettings.fSpeedGoToDocking) * i_ref_stSettings.fTravelTollerance * 1000);
		//??		s_cmPalletConveyor.m_StartForward(i_rTargetSpeed := i_ref_stSettings.fSpeedGoToDocking);
		//??	END_IF
		//??ELSE
		//??	s_eSeqTransport := E_SeqTransport.STOP;
		//??	//--s_cmPalletConveyor.m_Stop();
		//??END_IF
		
		IF s_bStaAtDockingPosition THEN
			s_eTmpLocation := E_Location.DOCKING;
			s_eSequence := E_SeqTransport.STOP;
		ELSIF s_fbSeqControl.q_tmStepTime > s_tMaxTravelTime THEN
			s_bErrTravelTimeOut := TRUE;
			s_sReason := 'Moving to docking - timeout';
			s_eSequence := E_SeqTransport.ERROR;
		END_IF
		
//STOP/////////////////////////////////////////////////////////////////////////////////////////////
	E_SeqTransport.STOP:
		IF s_fbSeqControl.q_bStaEntry THEN
			s_cmPalletConveyor.m_Stop();
		END_IF
		
		IF (i_eCtrlState = E_CtrlState.RUNNING) THEN
			IF s_cmPalletConveyor.p_bStaMotorStopped THEN
				s_eSequence := E_SeqTransport.READY;
			END_IF
		ELSE
			IF i_eCtrlState = E_CtrlState.IDLE THEN
				s_eSequence := E_SeqTransport.START;
			END_IF
		END_IF
		
//ERROR////////////////////////////////////////////////////////////////////////////////////////////
	E_SeqTransport.ERROR:
		s_cmPalletConveyor.m_Stop();
		s_cmCenteringUnit.m_Stop();
		
END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="m_Main" Id="{aa6670a8-50a5-40e7-bcf1-bbb60244f9de}" FolderPath="Base\">
      <Declaration><![CDATA[METHOD PUBLIC m_Main : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT SUPER^.m_Main() THEN
	RETURN;
END_IF

CASE i_eCtrlMode OF

	E_CtrlMode.IDLE:
		s_eSequence := E_SeqTransport.IDLE;
//--		s_eSeqCentering := E_SeqCentering.IDLE;
		
	E_CtrlMode.AUTOMATIC:
		//Automatic commands are controlled by higher-layer unit
		_m_AutomaticControl();
		
	E_CtrlMode.MANUAL:
		//Execute manaual control (based on HMI buttons)
		
		//Store the selected action (so it could be rstart / resumed if two-hand-control is activated)
		{warning '#TODO: Could be omitted if 2HCtrl is not used'}
		IF s_fbHmiBtnTB_ToInfeed.p_osrBtnPress THEN
			s_eActionSelection := E_ActionSelection.MOVE_TO_INFEED;
		ELSIF s_fbHmiBtnTB_ToCentering.p_osrBtnPress THEN
			s_eActionSelection := E_ActionSelection.MOVE_TO_CENTER;
		ELSIF s_fbHmiBtnTB_ToDocking.p_osrBtnPress THEN
			s_eActionSelection := E_ActionSelection.MOVE_TO_DOCKING;
		ELSIF s_fbHmiBtnTB_Centering.p_osrBtnPress THEN
			s_eActionSelection := E_ActionSelection.CENTER_TB;
		END_IF
		
		//Manual commands (convert HMI buttons to requests), only in state running
		IF (i_eCtrlState = E_CtrlState.RUNNING) THEN
			s_bReqTB_ToInfeed := s_fbHmiBtnTB_ToInfeed.p_osrBtnPress;
			s_bReqTB_ToCenter := s_fbHmiBtnTB_ToCentering.p_osrBtnPress;
			s_bReqTB_ToDocking := s_fbHmiBtnTB_ToDocking.p_osrBtnPress;
			s_bReqCenteringTB := s_fbHmiBtnTB_Centering.p_osrBtnPress;
		
		//??	s_bReqTB_ToInfeed := (eActionSelection = E_ActionSelection.MOVE_TO_INFEED) AND i_bSta2HC_Active;
		//??	s_bReqTB_ToCenter := (eActionSelection = E_ActionSelection.MOVE_TO_CENTER) AND i_bSta2HC_Active;
		//??	s_bReqTB_ToDocking := (eActionSelection = E_ActionSelection.MOVE_TO_DOCKING) AND i_bSta2HC_Active;
		//??	s_bReqCenteringTB := (eActionSelection = E_ActionSelection.CENTER_TB) AND i_bSta2HC_Active;
		END_IF
		
		_m_AutomaticControl();
		
	E_CtrlMode.MAINTENANCE:
		//Execute maintenance control (HMI control module buttons)
		s_eSequence := E_SeqTransport.IDLE;
//--		s_eSeqCentering := E_SeqCentering.IDLE;
		; 	//Maintenance control is implemented in lower-layer control modules
		
	E_CtrlMode.IO:
		//Execute IO-test control (HMI output force control)
		; 	//IO-Test control is implemented in lower-layer control modules
			//Enabling control is done direcly under this case instruction
		
	E_CtrlMode.CLEANING:
		//Execute cleaning control (cleaning  specific functionalities)
		;	//Cleaning control is not implemented, this could be a done in the future if neccesary
		
END_CASE

//AUTOMATIC-mode //////////////////////////////////////////////////////////////////////////////////

//MANUAL-mode /////////////////////////////////////////////////////////////////////////////////////

//Buttons visibility
IF (i_eCtrlMode = E_CtrlMode.MANUAL) THEN
	s_fbHmiBtnTB_ToInfeed.p_bStaVisible := TRUE;
	s_fbHmiBtnTB_ToCentering.p_bStaVisible := TRUE;
	s_fbHmiBtnTB_ToDocking.p_bStaVisible := TRUE;
	s_fbHmiBtnTB_Centering.p_bStaVisible := TRUE;
ELSE
	s_fbHmiBtnTB_ToInfeed.p_bStaVisible := FALSE;
	s_fbHmiBtnTB_ToCentering.p_bStaVisible := FALSE;
	s_fbHmiBtnTB_ToDocking.p_bStaVisible := FALSE;
	s_fbHmiBtnTB_Centering.p_bStaVisible := FALSE;
END_IF

//Buttons enabling
IF i_bReqEnable AND (i_eCtrlMode = E_CtrlMode.MANUAL) AND (i_eCtrlState = E_CtrlState.RUNNING) THEN
	
	//Transport
	s_fbHmiBtnTB_ToInfeed.p_bStaEnabled :=
		NOT(s_bIlckConveyorBackward)
		AND NOT(s_bStaAtInfeedRearPosition) //Added extra condition here instead of interlock only 
		AND NOT(s_cmPalletConveyor.p_bStaError)
		AND (s_cmPalletConveyor.p_bStaMotorStopped) AND TRUE
		AND NOT(s_bReqCenteringTB);
		
	s_fbHmiBtnTB_ToCentering.p_bStaEnabled :=
		NOT(s_bIlckConveyorForward)
		AND NOT(s_bStaAtCenteringPosition) //Added extra condition here instead of interlock only
		AND NOT(s_cmPalletConveyor.p_bStaError)
		AND (s_cmPalletConveyor.p_bStaMotorStopped)
		AND NOT(s_bReqCenteringTB);
		
	s_fbHmiBtnTB_ToDocking.p_bStaEnabled :=
		NOT(s_bIlckConveyorForward)
		AND NOT(s_bStaAtDockingPosition) //Added extra condition here instead of interlock only
		AND NOT(s_cmPalletConveyor.p_bStaError)
		AND (s_cmPalletConveyor.p_bStaMotorStopped)
		AND NOT(s_bReqCenteringTB);
		
	//Centering
	s_fbHmiBtnTB_Centering.p_bStaEnabled :=
		NOT(s_bIlckCenteringToCenter)
		AND NOT(s_cmCenteringUnit.p_bStaError)
		AND (s_eSequence = E_SeqTransport.READY); 
ELSE
	s_fbHmiBtnTB_ToInfeed.p_bStaEnabled := FALSE;
	s_fbHmiBtnTB_ToCentering.p_bStaEnabled := FALSE;
	s_fbHmiBtnTB_ToDocking.p_bStaEnabled := FALSE;
	s_fbHmiBtnTB_Centering.p_bStaEnabled := FALSE;
END_IF

//MAINTENANCE-mode ////////////////////////////////////////////////////////////////////////////////

//Stopping control modules
IF (i_eCtrlMode = E_CtrlMode.MAINTENANCE) AND (i_eCtrlState = E_CtrlState.STOPPING) THEN
	s_cmPalletConveyor.m_Stop();
END_IF

//IO-mode /////////////////////////////////////////////////////////////////////////////////////////
IF (i_eCtrlMode = E_CtrlMode.IO) AND (i_eCtrlState = E_CtrlState.RUNNING) THEN
	s_bHmiForceOutputEnabled := TRUE;
	s_bHmiForceOutputStartEnabled := s_cmPalletConveyor.p_bStaBrakeReleasedQ;
ELSE
	s_bHmiForceOutputEnabled := FALSE;
	s_bHmiForceOutputStartEnabled := FALSE;
END_IF

m_Main := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="m_ReqCenteringTB" Id="{40732af1-5968-493e-9f1c-3d3cc7dac1e7}">
      <Declaration><![CDATA[METHOD PUBLIC m_ReqCenteringTB : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

s_bReqCenteringTB := TRUE;

m_ReqCenteringTB := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="m_ReqTB_ToCenter" Id="{b0d9aff3-a553-4249-a74b-927274e40abb}">
      <Declaration><![CDATA[METHOD PUBLIC m_ReqTB_ToCenter : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

s_bReqTB_ToCenter := TRUE;

m_ReqTB_ToCenter := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="m_ReqTB_ToDocking" Id="{9af0ccf0-14a2-4cfb-b8b5-e7598f34e07a}">
      <Declaration><![CDATA[METHOD PUBLIC m_ReqTB_ToDocking : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

s_bReqTB_ToDocking := TRUE;

m_ReqTB_ToDocking := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="m_ReqTB_ToInfeed" Id="{3a45ec55-e220-4fd6-90dd-e8bfe1f36585}">
      <Declaration><![CDATA[METHOD PUBLIC m_ReqTB_ToInfeed : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT i_bReqEnable THEN
	RETURN;
END_IF

s_bReqTB_ToInfeed := TRUE;

m_ReqTB_ToInfeed := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="p_bStaReady" Id="{ed062b4f-ee76-405f-aa86-6b025ba3baa4}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
PROPERTY PUBLIC p_bStaReady : BOOL]]></Declaration>
      <Get Name="Get" Id="{00788f39-38ac-4700-b994-130408554d7c}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
p_bStaReady := (s_eSequence = E_SeqTransport.READY);
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="p_bStaTB_AtCentering" Id="{6ddc203e-db92-40f7-abd0-cebb67300ea0}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
PROPERTY PUBLIC p_bStaTB_AtCentering : BOOL]]></Declaration>
      <Get Name="Get" Id="{fceacb48-d0b5-4090-bcfc-e9341b4abb4d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
p_bStaTB_AtCentering := s_bStaTB_AtCentering;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="p_bStaTB_AtDocking" Id="{64dbd02e-e007-46ba-95b2-4e3e6585c1f4}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
PROPERTY PUBLIC p_bStaTB_AtDocking : BOOL]]></Declaration>
      <Get Name="Get" Id="{434e6867-93c8-430c-bdbd-fb758022bf84}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
p_bStaTB_AtDocking := s_bStaTB_AtDocking;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="p_bStaTB_AtInfeed" Id="{540ac9d4-eb8c-44e5-b977-7ebebe7b7564}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
PROPERTY PUBLIC p_bStaTB_AtInfeed : BOOL]]></Declaration>
      <Get Name="Get" Id="{f317c9e3-1584-4e85-aa27-2e64dc06b132}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
p_bStaTB_AtInfeed := s_bStaTB_AtInfeed;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="p_bStaTB_CenteringDone" Id="{f603121e-6c29-4c29-ab2d-75de3ab9a540}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
PROPERTY PUBLIC p_bStaTB_CenteringDone : BOOL]]></Declaration>
      <Get Name="Get" Id="{e3c67ae9-7f98-40da-87a2-bc324c4a547e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
p_bStaTB_CenteringDone := s_bStaTB_CenteringDone;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="p_bStaTB_DockPosFree" Id="{95a98749-377e-4138-b13d-5a64acc526a0}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
PROPERTY PUBLIC p_bStaTB_DockPosFree : BOOL]]></Declaration>
      <Get Name="Get" Id="{9d204aa9-a08b-40d3-abc9-8f70e1642d4f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
p_bStaTB_DockPosFree := NOT(s_bStaTB_AtDocking) AND NOT(s_bStaTB_AtCentering);
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="p_bStaTB_ReadyForDocking" Id="{470cd7f8-6c42-433f-95dd-bd36b50023ec}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
PROPERTY PUBLIC p_bStaTB_ReadyForDocking : BOOL]]></Declaration>
      <Get Name="Get" Id="{06be3b23-a74d-4bdd-9ece-42d84e930e24}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
p_bStaTB_ReadyForDocking := s_bStaTB_AtDocking AND s_cmPalletConveyor.p_bStaMotorStopped;
]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>